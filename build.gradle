buildscript {

	ext {
		kotlinVersion = '1.7.10'
		springBootVersion = '2.7.1'
		springKafkaVersion = '2.8.7'
		cxfVersion = '3.5.1'
		jacksonVersion = '2.13.3'
		oidcTokenSupportVersion = '2.0.14'
		logstashLogbackVersion = '7.2'
		pensjonInformasjonVersion = '9.4.10'
		mockserverVersion = '5.13.2'
		epMetricsVersion = '0.5.1'
		epLoggingVersion = '1.0.28'
		epPersonoppslagVersion = '11.1.1'
		epEuxVersion = '1.2.0'
	}

	repositories {
		mavenCentral()
	}
}

plugins {
	id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
	id "org.jetbrains.kotlin.plugin.spring" version "$kotlinVersion"
	id "org.jetbrains.kotlin.plugin.allopen" version "${kotlinVersion}"
	id 'org.springframework.boot' version "${springBootVersion}"
	id "io.spring.dependency-management" version "1.0.12.RELEASE"
	id 'com.github.ben-manes.versions' version '0.42.0'
	id 'se.patrikerdes.use-latest-versions' version '0.2.18'
	id "org.owasp.dependencycheck" version "7.1.1"
	id 'com.adarshr.test-logger' version '3.2.0'
	id "org.openrepose.gradle.plugins.jaxb" version "2.5.0"
	id "org.sonarqube" version "3.4.0.2513"
	id 'jacoco'
}

group = 'no.nav.eessi.pensjon'

configurations {
	xsd
}

dependencies {

	// Spring-boot BOM-fil spesifiserer versjoner for Spring Boot-avhengigheter
	implementation(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
	implementation("org.springframework.boot:spring-boot-starter-aop")
	implementation("org.springframework.boot:spring-boot-starter-web")
	implementation("org.springframework.boot:spring-boot-starter-actuator")
	implementation("org.springframework.boot:spring-boot-actuator")

	// Spring Framework BOM-fil kan overstyre så vi får siste patch-versjon av Spring Framework
	implementation(platform("org.springframework:spring-framework-bom:5.3.21"))

	implementation("org.apache.httpcomponents:httpclient:4.5.13")

	implementation('org.springframework.retry:spring-retry:1.3.3')

	implementation "com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonVersion}"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"

	implementation(platform("org.jetbrains.kotlin:kotlin-bom:$kotlinVersion"))
	implementation 'org.jetbrains.kotlin:kotlin-reflect'

	implementation "org.springframework.kafka:spring-kafka:$springKafkaVersion"
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude module: 'junit'
		exclude module: "mockito-core"
		exclude module: "junit-vintage-engine"
	}
	testImplementation "org.springframework.kafka:spring-kafka-test:$springKafkaVersion"

	testImplementation(platform("org.junit:junit-bom:5.8.2"))
	testImplementation("org.junit.jupiter:junit-jupiter")

	//mock - test
	testImplementation("org.mock-server:mockserver-netty:${mockserverVersion}") {
		exclude module: 'junit'
	}
	testImplementation("org.mock-server:mockserver-client-java:${mockserverVersion}")
	testImplementation("io.mockk:mockk:1.12.4")
	testImplementation("com.ninja-squad:springmockk:3.1.1")

	// Architecture tests
	testImplementation 'com.tngtech.archunit:archunit:0.23.1'

	// Eessi pensjon biblioteker
	implementation("no.nav.eessi.pensjon:ep-metrics:${epMetricsVersion}")
	implementation("no.nav.eessi.pensjon:ep-logging:${epLoggingVersion}")
	implementation("no.nav.eessi.pensjon:ep-personoppslag:${epPersonoppslagVersion}")
	// Rina Eux API
	implementation("no.nav.eessi.pensjon:ep-eux:${epEuxVersion}")
	// PDF box
	implementation("org.apache.pdfbox:pdfbox-tools:2.0.25")
	implementation("com.twelvemonkeys.imageio:imageio-jpeg:3.8.2")
	implementation("com.twelvemonkeys.imageio:imageio-tiff:3.8.2")

	// Logging
	implementation("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")
	configurations { all*.exclude group: 'commons-logging', module: 'commons-logging' }
	implementation group: 'org.slf4j', name: 'jcl-over-slf4j'

	//Auth
	//Auth
	implementation("no.nav.security:token-client-spring:${oidcTokenSupportVersion}")
	implementation("no.nav.security:token-validation-spring:${oidcTokenSupportVersion}")
	implementation("no.nav.security:token-validation-jaxrs:${oidcTokenSupportVersion}")
	implementation("no.nav.security:token-validation-test-support:2.0.5")

}

test {
	useJUnitPlatform()
	filter {
		//exclude all tests from a package.
		excludeTestsMatching "no.nav.eessi.pensjon.integrasjonstest.*"
	}
	failFast = true
	testLogging {
		exceptionFormat "full"
		events "started", "skipped", "passed", "failed"
		showStandardStreams true
	}
}

assert JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17): "Java 17 or newer is required"

group = 'no.nav.eessi'

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

compileKotlin {
	kotlinOptions {
		jvmTarget = "17"
	}
}

compileTestKotlin {
	kotlinOptions {
		jvmTarget = '17'
	}
}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacoco {
	toolVersion = "0.8.8"
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

tasks.sonarqube.dependsOn tasks["jacocoTestReport"]

repositories {
	mavenCentral()

	String token = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key")
	if (!token) throw new NullPointerException("Missing token, you have to set GITHUB_TOKEN or gpr.key, see README")

	maven {
		url = uri("https://maven.pkg.github.com/navikt/maven-release")
		credentials {
			username = "token"
			password = token
		}
	}
}

wrapper {
	distributionType = Wrapper.DistributionType.ALL
}



sonarqube {
	properties {
		property 'sonar.host.url', 'http://localhost:9000'
	}
}


dependencyUpdates {
	rejectVersionIf {
		['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'pr']
				.any { qualifier -> it.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/}
	}
	revision = 'release'
}


