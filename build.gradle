buildscript {

	ext {
		kotlinVersion = '1.6.10'
		springBootVersion = '2.6.1'
		cxfVersion = '3.4.5'
		oidcTokenSupportVersion = '1.3.9'
		logstashLogbackVersion = '7.0.1'
		pensjonInformasjonVersion = '9.4.10'
		mockserverVersion = '5.11.2'
		epMetricsVersion = '0.4.12'
		epLoggingVersion = '1.0.15'
		epSecurityStsVersion = '0.0.21'
		epPersonoppslagVersion = '9.0.6'
		epEuxVersion = '1.1.58'
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
		classpath("gradle.plugin.org.openrepose:gradle-jaxb-plugin:2.5.0")
	}
}

plugins {
	id 'com.github.ben-manes.versions' version '0.39.0'
	id 'se.patrikerdes.use-latest-versions' version '0.2.18'
	id "org.owasp.dependencycheck" version "6.5.0.1"
	id 'com.adarshr.test-logger' version '3.1.0'
	id "org.sonarqube" version "3.3"
	id 'jacoco'
}

assert JavaVersion.current().isJava11Compatible(): "Java 11 or newer is required"

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.openrepose.gradle.plugins.jaxb'

group = 'no.nav.eessi.pensjon'

configurations {
	xsd
}

dependencies {

	// Spring-boot
	implementation("org.springframework.boot:spring-boot-starter-aop:${springBootVersion}")
	implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
	implementation("org.apache.httpcomponents:httpclient:4.5.13")


	implementation("org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}")
	implementation("org.springframework.boot:spring-boot-actuator:${springBootVersion}")

	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.0'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	implementation 'org.springframework.kafka:spring-kafka'
    testImplementation('org.springframework.boot:spring-boot-starter-test:2.6.1') {
		exclude module: 'junit'
		exclude module: "mockito-core"
		exclude module: "junit-vintage-engine"
	}
	testImplementation 'org.springframework.kafka:spring-kafka-test'

	// Apache CXF
	implementation("org.apache.cxf:cxf-spring-boot-starter-jaxws:${cxfVersion}") {
		exclude module: 'spring-boot-starter-web'
	}

	implementation("org.apache.cxf:cxf-rt-ws-security:${cxfVersion}")

	//mock - test
	testImplementation("org.jetbrains.kotlin:kotlin-test:${kotlinVersion}") {
		exclude module: 'kotlin-test-js'
		exclude module: 'kotlin-test-testng'
	}
	testImplementation("org.mock-server:mockserver-netty:${mockserverVersion}") {
		exclude module: 'junit'
	}
	testImplementation("org.mock-server:mockserver-client-java:${mockserverVersion}")
	testImplementation("io.mockk:mockk:1.12.1")
	testImplementation("com.ninja-squad:springmockk:3.0.1")

	// Architecture tests
	testImplementation 'com.tngtech.archunit:archunit:0.22.0'

	// Micrometer
	implementation("io.micrometer:micrometer-registry-prometheus:1.8.1")

	// Eessi pensjon biblioteker
	implementation("no.nav.eessi.pensjon:ep-metrics:${epMetricsVersion}")
	implementation("no.nav.eessi.pensjon:ep-logging:${epLoggingVersion}")
	implementation("no.nav.eessi.pensjon:ep-personoppslag:${epPersonoppslagVersion}")
	// Rina Eux API
	implementation("no.nav.eessi.pensjon:ep-eux:${epEuxVersion}") {
		exclude module: 'ep-security-sts'
	}
	// PDF box
	implementation("org.apache.pdfbox:pdfbox-tools:2.0.24")
	implementation("com.twelvemonkeys.imageio:imageio-jpeg:3.8.0")
	implementation("com.twelvemonkeys.imageio:imageio-tiff:3.8.0")

	// Logging
	implementation("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")
	configurations { all*.exclude group: 'commons-logging', module: 'commons-logging' }
	implementation group: 'org.slf4j', name: 'jcl-over-slf4j'

	//Auth
	//Auth
	implementation("no.nav.security:token-client-spring:${oidcTokenSupportVersion}")
	implementation("no.nav.security:token-validation-spring:${oidcTokenSupportVersion}")
	implementation("no.nav.security:token-validation-jaxrs:${oidcTokenSupportVersion}")
	implementation("no.nav.security:token-validation-test-support:${oidcTokenSupportVersion}")

}

test {
	useJUnitPlatform()
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-Xjsr305=strict"]
		jvmTarget = "11"
	}
}

compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '11'
	}
}

repositories {
	mavenCentral()

	String token = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key")
	if (!token) throw new NullPointerException("Missing token, you have to set GITHUB_TOKEN or gpr.key, see README")

	maven {
		url = uri("https://maven.pkg.github.com/navikt/maven-release")
		credentials {
			username = "token"
			password = token
		}
	}
}

wrapper {
	distributionType = Wrapper.DistributionType.ALL
}


jacocoTestReport {
	reports {
		xml.enabled true
	}
}

sonarqube {
	properties {
		property 'sonar.host.url', 'http://localhost:9000'
	}
}
tasks.sonarqube.dependsOn tasks["jacocoTestReport"]


dependencyUpdates {
	rejectVersionIf {
		['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'pr']
				.any { qualifier -> it.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/}
	}
	revision = 'release'
}


